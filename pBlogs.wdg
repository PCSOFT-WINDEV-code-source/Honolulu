#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : pBlogs
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x155352d8026a4185
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  internal_properties : CAAAAAgAAACRmWIV9vYFGO1p7ZJ3n8CrdwJlDt1O+DyXWo+ODpW3p84BqhgjR34BQJ4vQ6zneBQcCI7JnbwyHcwwgtr4nk2d+QqbBav2muPDOZfSfk4zhvsqqfQRs9Y8ok72X6eeUDuvwGZjlF20ilDSon7L2Uli4HQ=
  type_code : 31
  p_codes :
   -
     internal_properties : CAAAAAgAAAD1PyOnfWan5j/8P1oc55hZm+1s3Chts5NHKpt4yAWPF5EUNsCbOFkNWDOT1PC+RS5/iuqg0BTmKHqBZDnUnGqIIcX4D8nY/ExgAn6I6q6kLh8trJfeB0pLjAxZcGsNowg5yRvVthhf+UUYyZI=
     code : |1-
      // Collection de procédures de gestion des blogs
      
      constant
      	// Compteurs
      	CPT_BILLETS = "TotalBilletsBlog"
      	CPT_COMMENTAIRES_BLOG = "TotalCommentairesBlog"
      	CPT_COMMENTAIRES_BILLET = "TotalCommentairesBillet"
      	
      	// Notifications
      	NOTIFICATION_BLOGS_COMMENTAIRES = "NOTIFICATION_BLOGS_COMMENTAIRES"
      	NOTIFICATION_BLOGS_CREATIONBILLET = "NOTIFICATION_BLOGS_CREATIONBILLET"
      FIN
      
      // Branche des triggers sur l'ajout/suppression de billets ou de commentaires pour la mise à jour des compteurs
      si pas HDécritTrigger(billet, 		"HAjoute,HSupprime", 	TRIGGER_AjoutSuppressionBillets, 		htriggerapres) alors dbgAssertion(Faux, HErreur)
      SI PAS HDécritTrigger(Commentaire, 	"HAjoute,HSupprime", 		TRIGGER_AjoutSuppressionCommentaires, 	hTriggerAprès) ALORS dbgAssertion(Faux, HErreur)
      SI PAS HDécritTrigger(blog, 		"HSupprime", 	TRIGGER_AjoutSuppressionBlog, 			hTriggerAprès) ALORS dbgAssertion(Faux, HErreur)
     type : 720896
  procedures :
   -
     name : TRIGGER_AjoutSuppressionBillets
     internal_properties : CAAAAAgAAACYE4Ri9ORb4bbGOyVqmou+w/DkPNIPea707oDFBaASH5IHgQzJlsRlPGRWSHailWqIcDQltsKBg6QwgT78aKzRg/QqggTe05KqIv7WIOkaQWK/JYHZopc08N048tcwoouHTu1/oGXpIhQHAqlQ1NU8yb9LjnSJLFMMgcCqqZ0jEvok1g57KimUeF1P7aDgGax6IjXIW3A=
     procedure_id : 1536672425951808342
     type_code : 15
     code : |1-
      // Résumé : Trigger HFSQL appelé lors d'un ajout ou d'une suppression d'un billet
      // Syntaxe :
      //TRIGGER_AjoutSuppressionBillets ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      Procédure RESTREINTE TRIGGER_AjoutSuppressionBillets()
      
      
      selon h.FonctionTrigger
      	cas ~= "HAjoute"
      		
      		// Incrémente le compteur de billets
      		CompteurIncrémente(CPT_BILLETS + Billet.IDBlog)	
      
      		sGUID est une chaine = HFSauvePosition(blog..nom)
      		
      		HLitRecherchePremier(blog, idblog, billet.idblog)
      		
      		// Notification
      		NotificationEnvoie(	MODULE_blogs, ...
      			NOTIFICATION_BLOGS_CREATIONBILLET, ...
      			<§@155352d5026836790001§>, ...
      			ChaîneConstruit(<§@155352d5026836790006§>, RécupèreLogin(Billet.IDUtilisateur), Billet.Titre, Blog.Titre))
      			
      		HFRetourPosition(sGUID)
      		
      	cas ~= "HSupprime"
      
      		// Décrémente le compteur de billets
      		Compteurdécrémente(CPT_BILLETS + Billet.IDBlog)		
      		
      		// Supprime le compteur de commentaires pour ce billet
      		ParamètreSupprime(CPT_COMMENTAIRES_BILLET + Billet.IDBillet)		
      				
      	AUTRES CAS
      		dbgAssertion(Faux,<§@155352d5026836790007§>+H.FonctionTrigger)		
      FIN
     type : 458752
   -
     name : TRIGGER_AjoutSuppressionCommentaires
     internal_properties : CAAAAAgAAABs1OGt9XMdNcq1alTZPsXib2oZJ0LhOZV4uByU93MOU+Kato4tggA4CmiRAtrzC338ZZe6LT0lbOcKJtyMsnKIax+YbSSpRf0I98mR7xtf2+Rv1HoaTR4t5cq1ddITp8bUA5TgQYQMt//yDQ6XcfajOii+c62GbYJ/PNtjgKAMeahqrFAtWqcSyOWXdVBUzSbG9geenTkr45Z9s2/8jyM37TZWipoH
     procedure_id : 1536673001481229709
     type_code : 15
     code : |1-
      // Résumé : Trigger HFSQL appelé lors d'un ajout ou d'une suppression d'un commentaire
      // Syntaxe :
      //TRIGGER_AjoutSuppressionCommentaires ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      Procédure RESTREINTE TRIGGER_AjoutSuppressionCommentaires()
      
      GUID est une chaîne
      
      selon h.FonctionTrigger
      	cas ~= "HAjoute"
      		
      		// Incrémente le compteur de commentaires pour le billet
      		CompteurIncrémente(CPT_COMMENTAIRES_BILLET + Commentaire.IDBillet)	
      
      		GUID = HFSauvePosition(billet..nom + ";" + blog..nom)
      		
      		HLitRecherchePremier(Billet, IDBillet, Commentaire.IDBillet)
      		HLitRecherchePremier(blog, idblog, Billet.IDBlog)
      
      		// Incrémente le compteur de commentaires pour le blog			
      		CompteurIncrémente(CPT_COMMENTAIRES_BLOG + Billet.IDBlog)	
      		
      		//
      		// On profite de ce trigger pour le suivi de commentaires d'un billet
      		//
      		
      		// Vérifie si l'auteur du billet a demandé à suivre les commentaires 
      		si billet.SuiviDesCommentairesActif alors
      			
      			// Uniquement si l'auteur du commentaire n'est pas lui même
      			si commentaire.IDUtilisateur <> billet.IDUtilisateur ALORS
      				
      				// Notification
      				NotificationEnvoie(MODULE_BLOGS, NOTIFICATION_BLOGS_COMMENTAIRES, ...
      										ChaîneConstruit(<§@155352d5026836790008§>, Blog.Titre), ...
      										ChaîneConstruit(<§@155352d5026836790000§>, billet.titre, RécupèreNomUtilisateur(Commentaire.IDUtilisateur)), ...
      										Billet.IDUtilisateur)
      				
      			FIN
      		FIN
      		
      		HFRetourPosition(GUID)
      		
      		
      	cas ~= "HSupprime"
      
      		// Décrémente le compteur de billets
      		CompteurDécrémente(CPT_COMMENTAIRES_BILLET + Commentaire.IDBillet)		
      
      		GUID = HFSauvePosition(Billet..Nom)
      		HLitRecherchePremier(Billet, IDBillet, Commentaire.IDBillet)
      
      		// Décrémente le compteur de commentaires pour le blog
      		CompteurDécrémente(CPT_COMMENTAIRES_BLOG + Billet.IDBlog)
      		
      		HFRetourPosition(GUID)
      				
      	AUTRES CAS
      		dbgAssertion(Faux,<§@155352d5026836790007§>+H.FonctionTrigger)		
      FIN
     type : 458752
   -
     name : TRIGGER_AjoutSuppressionBlog
     internal_properties : CAAAAAgAAACmx9qgJMzZ7LBIaW+WBoE64UDKd4iyly1aQBH/05i+UPqW3kyEbuIhlteYf9BfCesFskLuvKKtoLwdvKggcrQYM95bORbYWjrFsrL/M1g5f7ak9r2SNOOU415Oj4jcsOlt0PKRXYNPnB1+VDhDbIcWNZfyUqf9wMPpZOZFGn1ngkdN6HU5G17nooXyA0I1DecLRfZGdjva5lUKdFy7g8k1B8Ir3A==
     procedure_id : 1562247891406811643
     type_code : 15
     code : |1-
      // Résumé : Trigger déclenché lors de l'ajout / suppression d'un blog
      // Syntaxe :
      //TRIGGER_AjoutSuppressionBlog ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      Procédure RESTREINTE TRIGGER_AjoutSuppressionBlog()
      
      // Variables locales
      sListeClé 		est une chaine
      sClé 			est une chaine
      
      selon h.FonctionTrigger
      
      	cas ~= "HSupprime"
      	
      		// Suppression des notifications actives pour ce blog
      		sListeClé = ParamètreListe(PourTous, NOTIFICATION_BLOGS_CREATIONBILLET + "_" + MODULE_BLOGS + Blog.IDBlog)
      		POUR TOUTE CHAINE sClé DE sListeClé SEPAREE PAR RC
      			
      			// Supprime la notification
      			ParamètreSupprime(sClé)
      			
      		FIN
      		
      	AUTRES CAS
      		dbgAssertion(Faux,<§@155352d5026836790007§>+H.FonctionTrigger)		
      FIN
     type : 458752
   -
     name : BlogNotificationActive
     procedure_id : 1562652193532567363
     type_code : 15
     code : |1-
      // Résumé : Vérifie si une notification de création de billet dans un blog est active pour un utilisateur
      // Syntaxe :
      //[ <Résultat> = ] BlogNotificationActive (<nIDBlog> [, <nIDUtilisateur>])
      //
      // Paramètres :
      //	nIDBlog : Identifiant du sujet
      //	nIDUtilisateur : Identifiant de l'utilisateur
      // Valeur de retour :
      // 	Type indéterminé : Vrai si une notification est active, Faux sinon
      //
      Procédure BlogNotificationActive(nIDBlog, nIDUtilisateur = UtilisateurEnCours:IDUtilisateur)
      
      // On doit être connecté
      SI PAS EstConnecté() ALORS renvoyer 0
      
      renvoyer val(ParamètreLit(NOTIFICATION_BLOGS_CREATIONBILLET + "_" + MODULE_BLOGS + nIDBlog + "_" + nIDUtilisateur, pourtous, "0"))
      	
     type : 458752
   -
     name : BlogNotificationCrée
     procedure_id : 1562652193532632961
     type_code : 15
     code : |1-
      // Résumé : Crée une notification de création de billet dans un blog pour un utilisateur
      // Syntaxe :
      //BlogNotificationCrée (<nIDBlog> [, <nIDUtilisateur>])
      //
      // Paramètres :
      //	nIDBlog : Identifiant du sujet
      //	nIDUtilisateur : Identifiant de l'utilisateur
      // Valeur de retour :
      // 	Aucune
      //
      Procédure BlogNotificationCrée(nIDBlog, nIDUtilisateur = UtilisateurEnCours:IDUtilisateur)
      
      // On doit être connecté
      SI PAS EstConnecté() ALORS RETOUR
      
      ParamètreEcrit(NOTIFICATION_BLOGS_CREATIONBILLET + "_" + MODULE_BLOGS + nIDBlog + "_" + nIDUtilisateur, "1")
      	
     type : 458752
   -
     name : BlogNotificationSupprime
     procedure_id : 1562652193532698544
     type_code : 15
     code : |1-
      // Résumé : Supprime une notification de création de réponse sur un sujet pour un utilisateur
      // Syntaxe :
      //BlogNotificationSupprime (<nIDBlog> [, <nIDUtilisateur>])
      //
      // Paramètres :
      //	nIDBlog : Identifiant du sujet
      //	nIDUtilisateur : Identifiant de l'utilisateur
      // Valeur de retour :
      // 	Aucune
      //
      Procédure BlogNotificationSupprime(nIDBlog, nIDUtilisateur = UtilisateurEnCours:IDUtilisateur)
      
      // On doit être connecté
      SI PAS EstConnecté() ALORS Retour
      
      Paramètresupprime(NOTIFICATION_BLOGS_CREATIONBILLET + "_" + MODULE_BLOGS + nIDBlog + "_" + nIDUtilisateur)
      	
     type : 458752
   -
     name : BlogLibellé
     procedure_id : 1562657068493549318
     type_code : 15
     code : |1+
      // Résumé : Renvoie le titre d'un blog
      // Syntaxe :
      //[ <Résultat> = ] BlogLibellé (<nIDBlog>)
      //
      // Paramètres :
      //	nIDBlog : Identifiant du blog
      // Valeur de retour :
      // 	chaîne : Titre du blog
      //
      Procédure BlogLibellé(nIDBlog)
      
      // Variables locales
      sLibellé 	est une chaine
      sGUID 		est une chaine
      
      // Sauvegarde du contexte
      sGUID = HFSauvePosition(blog..nom)
      	
      // Recherche du blog	
      HLitRecherchePremier(blog, idblog, nIDBlog)
      si htrouve(Blog) ALORS
      	sLibellé = blog.titre
      FIN
      
      // Restauration du contexte
      HFRetourPosition(sGUID)
      	
      renvoyer sLibellé
     type : 458752
   -
     name : BlogsMiseAJour
     procedure_id : 1582603755601846433
     type_code : 15
     code : |1+
      // Résumé : Mise à jour des compteurs des blogs
      // Syntaxe :
      //BlogsMiseAJour ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      Procédure RESTREINTE BlogsMiseAJour()
      
      	GUID est une chaîne = HFSauvePosition(forum..nom + ";" + topic..nom + ";" + post..nom)
      
      	HGèreTrigger(Faux)
      
      	// Vérification des cohérences de clés
      	pour tout Commentaire 
      		HRecherchePremier(billet, IDBillet, Commentaire.IDBillet)
      		si pas htrouve(billet) alors			
      			hsupprime(Commentaire)
      			continuer
      		FIN
      		
      		HLitRecherchePremier(billet, idbillet, Commentaire.IDBillet)
      		HRecherchePremier(Blog, IDBlog, billet.IDBlog)
      		si pas htrouve(Blog) ALORS
      			// Supprime le commentaire
      			hsupprime(Commentaire)
      			// Supprime le billet
      			HSupprime(Billet)
      			continuer
      		FIN
      	FIN
      	
      	HGèreTrigger(vrai)
      
      	// Supprime tous les compteurs associés aux blogs
      	BlogCompteursSupprime()
      	
      	pour tout blog
      		
      		// Calcule le nombre de billets dans le blog
      		REQ_Blogs_TotalBillets.pIDBlog = blog.idblog
      		HExécuteRequête(REQ_Blogs_TotalBillets)
      		
      		// Stocke le total pour ce blog
      		CompteurIncrémente(CPT_BILLETS + blog.idblog, REQ_Blogs_TotalBillets.Total)
      		
      		// Calcule le nombre de commentaires dans le blog
      		REQ_Blogs_TotalCommentaires.pIDBlog = blog.idblog
      		REQ_Blogs_TotalCommentaires.pIDBillet = null
      		HExécuteRequête(REQ_Blogs_TotalCommentaires)
      
      		// Stocke le total pour ce blog
      		CompteurIncrémente(CPT_COMMENTAIRES_BLOG + Blog.IDBlog, REQ_Blogs_TotalCommentaires.Comptage_1)
      		
      		pour tout billet avec IDBlog = blog.idblog
      			
      			// Calcule le nombre de commentaires dans le billet
      			REQ_Blogs_TotalCommentaires.pIDBlog = null
      			REQ_Blogs_TotalCommentaires.pIDBillet = billet.idbillet
      			HExécuteRequête(REQ_Blogs_TotalCommentaires)
      			
      			// Stocke le total pour ce billet
      			CompteurIncrémente(CPT_COMMENTAIRES_BILLET + Billet.IDBillet, REQ_Blogs_TotalCommentaires.Comptage_1)
      			
      		FIN
      	
      	FIN	
      	
      	HfRetourPosition(GUID)
     type : 458752
   -
     name : BlogCompteursSupprime
     procedure_id : 1582603867277379574
     type_code : 15
     code : |1+
      // Résumé : Supprime tous les compteurs associés aux blogs
      // Syntaxe :
      //BlogCompteursSupprime ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      Procédure RESTREINTE  BlogCompteursSupprime()
      
      	sListeClé est une chaine
      	sClé est une chaine
      
      	// Récupère tous les compteurs enregistrés
      	sListeClé = ParamètreListe(PourTous, CPT_BILLETS, faux)
      	sListeClé += [RC] + ParamètreListe(PourTous, CPT_COMMENTAIRES_BILLET, Faux)
      	sListeClé += [RC] + ParamètreListe(PourTous, CPT_COMMENTAIRES_BLOG, Faux)
      	
      	// Supprime tous les compteurs
      	POUR TOUTE CHAINE sClé DE sListeClé SEPAREE PAR RC		
      		// Supprime le compteur
      		ParamètreSupprime(sClé)
      	FIN
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
  original_name : COL_Collection1
resources :
 string_res :
  identifier : 0x155352d502683679
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
  strings :
   -
     text :
      fr-FR : "Un nouveau commentaire a été déposé sur votre billet intitulé <I>'%1'</I>, par <B>%2</B>."
     index : 0
   -
     text :
      fr-FR : Nouveau billet dans un blog
     index : 1
   -
     text :
      fr-FR : "\t\t\t\r\n\t\t\t%1 vient d'écrire un nouveau billet dans le blog auquel vous êtes inscrit.\r\n\t\t\tTitre du billet : %2\t\t\t\r\n\t\t\tDans le blog : %3\r\n\t\t\t"
     index : 6
   -
     text :
      fr-FR : "Trigger non géré : "
     index : 7
   -
     text :
      fr-FR : "[%1] Nouveau commentaire déposé"
     index : 8
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
